apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    def lifecycle_version = "2.1.0"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // LocalBroadcastManager dependency
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    // Livedata dependencies
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"

}

def libraryGroupId = 'com.software.pasithea'
def libraryArtifactId = 'Pasithea'
def libraryVersion = '1.1.0-SNAPSHOT'

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId

            artifact("$buildDir/outputs/aar/app-release.aar")
        }
    }
}

artifactory {
    contextUrl = 'http://logicielpasithea.com:8081/artifactory'
    publish {
        repository {
            repoKey = libraryVersion.endsWith('SNAPSHOT') ? 'pasithea-snapshot-local' : libraryVersion.endsWith('TEST') ? 'pasithea-test-local' : 'pasithea-release-local'
            username = mavenuser
            password = mavenpasswd
            contextUrl = mavenURL
            defaults {
                    publications('aar')
                    publishArtifacts = true
                    properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
                    publishPom = true
            }
        }
    }
}
